
def abortUnauthorized(*gin.Context c) (
    string path = c.Request.URL.Path
    if path.startsWith("/api") (
        c.JSON(401, { ok: false, error: "not authenticated" })
    ) else (
        c.Redirect(302, "/auth")
    )
    c.Abort()
)

def requireSession(*gin.Context c) (
    string sessionId = c.Cookie("session_id").toArray()[1].toStr()
    
    if sessionId == "" (
        sessionId = c.GetHeader("sessionId")
        if sessionId == "" (
            abortUnauthorized(c)
            return
        )
    )

    if !sessions.contains(sessionId) (
        abortUnauthorized(c)
        return
    )
    string userId = sessions[sessionId]
    if !userData.contains(userId) (
        if fs.Exists("db/" ++ userId ++ "/user.json") (
            string data = fs.ReadFile("db/" ++ userId ++ "/user.json")
            userData[userId] = data.JsonParse()
        )
    )
    c.Set("userId", userId)
    string username = ""
    if userIdToUsername.contains(userId) (
        username = userIdToUsername[userId]
    ) else if userData.contains(userId) (
        object profile = userData[userId]
        if profile.username != null (
            username = profile.username.toStr()
        )
    )
    c.Set("username", username)
    c.Next()
)