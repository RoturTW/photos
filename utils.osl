def randomString(int length) string (
    string chars = "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789"
    string result = ""
    for i length (
        result = result ++ chars[rand.Intn(chars.len) + 1].toStr()
    )
    return result
)

def calculateFileHash(string path) string (
    string hash = ""
    raw("f, err := os.Open(path)")
    if err == null (
        raw("h := md5.New()")
        raw("if _, err := io.Copy(h, f); err == nil { hash = hex.EncodeToString(h.Sum(nil)) }")
        raw("f.Close()")
    )
    return hash
)

def noCORS(*gin.Context c) (
    c.Header("Access-Control-Allow-Origin", "*")
    string origin = c.GetHeader("Origin")
    if origin != "" (
        c.Header("Access-Control-Allow-Origin", origin)
        c.Header("Access-Control-Allow-Credentials", "true")
        c.Next()
        return
    )
    c.Header("Access-Control-Allow-Methods", "GET, POST, PUT, DELETE, OPTIONS")
    c.Header("Access-Control-Allow-Headers", "*")
    if c.Request.Method == "OPTIONS" (
        c.AbortWithStatus(204)
        return
    )
    c.Next()
)

def loadConfig() (
    object config = fs.ReadFile("config.json").JsonParse().assert(object)
    authKey = config.authKey.toStr()
    alwaysAllowedUsers = config.alwaysAllowedUsers
    useSubscriptions = config.useSubscriptions.toBool()
    subscriptionSizes = config.subscriptionSizes
    quotas = config.quotas
)

def getAble(string username) object (
    username = username.toLower()
    object profile = userData[username]
    if profile == null (
        if fs.Exists("db/" ++ username ++ "/user.json") (
            string data = fs.ReadFile("db/" ++ username ++ "/user.json")
            profile = data.JsonParse().assert(object)
        ) else (
            return { canAccess: false, maxUpload: "0" }
        )
    )
    string subscription = profile.subscription.toStr().toLower()
    number quota = 0
    any maybeQuota = quotas[username.toLower()]
    if maybeQuota != null (
        quota = maybeQuota.toNum()
    ) else if useSubscriptions (
        quota = subscriptionSizes[subscription].toNum()
    )

    array images = readUserImages(username)
    
    return {
        canAccess: (quota > 0),
        storageQuota: quota,
        hasImages: images.len > 0
    }
)

def getProfile(string username) object (
    object resp_profile = requests.Get("https://api.rotur.dev/profile?include_posts=0&name=" ++ username)
    if !resp_profile.success (
        return { ok: false, error: "failed to fetch profile" }
    )
    object profile = resp_profile.body.toStr().JsonParse()
    if profile.error != null (
        return { ok: false, error: profile.error }
    )
    return { ok: true, profile: profile }
)

def writeProfile(string username) object (
    object profileReq = getProfile(username)
    if !profileReq.ok (
        return profileReq
    )
    userData[username.toLower()] = profileReq.profile
    fs.MkdirAll("db/" ++ username.toLower())
    fs.WriteFile("db/" ++ username.toLower() ++ "/user.json", profileReq.profile.toStr())
    return profileReq
)

def userDbPath(string username) string (
    return "db/" ++ username.toLower() ++ "/images.json"
)

def ensureUserDb(string username) (
    string dir = "db/" ++ username.toLower()
    if !fs.Exists(dir) (
        fs.MkdirAll(dir)
    )
    string path = userDbPath(username)
    if !fs.Exists(path) (
        fs.WriteFile(path, "[]")
    )
)

def readUserImages(string username) array (
    ensureUserDb(username)
    string path = userDbPath(username)
    string data = fs.ReadFile(path)
    if data == "" (
        return []
    )
    array arr = data.JsonParse()
    return arr
)

def enrichImagesWithSharing(string username, array images) array (
    object sharesObj = readUserShares(username)
    array shares = sharesObj.shares
    for i images.len (
        object img = images[i]
        for j shares.len (
            object share = shares[j]
            if share.imageId.toStr() == img.id.toStr() (
                img["sharedWith"] = share.sharedWith
                img["isPublic"] = share.isPublic
            )
        )
    )
    return images
)

def writeUserImages(string username, array arr) boolean (
    string path = userDbPath(username)
    string out = arr.toStr()
    return fs.WriteFile(path, out)
)

def findImage(array arr, string id) object (
    for i arr.len (
        object it = arr[i]
        if it.id.toStr() == id (
            return it
        )
    )
    return {}
)

def removeImage(array arr, string id) array (
    array out = []
    for i arr.len (
        object it = arr[i]
        if it.id.toStr() != id (
            out.append(it)
        )
    )
    return out
)

def userAlbumsPath(string username) string (
    return "db/" ++ username.toLower() ++ "/albums.json"
)

def ensureUserAlbums(string username) (
    string dir = "db/" ++ username.toLower()
    if !fs.Exists(dir) (
        fs.MkdirAll(dir)
    )
    string path = userAlbumsPath(username)
    if !fs.Exists(path) (
        fs.WriteFile(path, "{ \"albums\": [], \"items\": {} }")
    )
)

def readUserAlbums(string username) object (
    ensureUserAlbums(username)
    string path = userAlbumsPath(username)
    string data = fs.ReadFile(path)
    if data == "" (
        return { albums: [], items: {} }
    )
    object obj = data.JsonParse()
    if obj.albums == null (
        obj.albums = []
    )
    if obj.items == null (
        obj.items = {}
    )
    return obj
)

def writeUserAlbums(string username, object albums) boolean (
    string path = userAlbumsPath(username)
    string out = albums.toStr()
    return fs.WriteFile(path, out)
)

def addAlbum(string username, string name) object (
    object albums = readUserAlbums(username)
    array list = albums.albums
    boolean exists = false
    for i list.len (
        if list[i].toStr().toLower() == name.toLower() (
            exists = true
        )
    )
    if !exists (
        list.append(name)
        albums.albums = list
    )
    if albums.items[name] == null (
        object items = albums.items
        items[name] = []
    )
    writeUserAlbums(username, albums)
    return albums
)

def removeAlbumDef(string username, string name) object (
    object albums = readUserAlbums(username)
    array list = albums.albums
    array out = []
    for i list.len (
        string it = list[i].toStr()
        if it.toLower() != name.toLower() (
            out.append(it)
        )
    )
    albums.albums = out
    albums.items.delete(name)
    writeUserAlbums(username, albums)
    return albums
)

def addImageToAlbum(string username, string name, string id) object (
    object albums = readUserAlbums(username)
    object items = albums.items
    if albums.items[name] == null (
        items[name] = []
    )
    array ids = items[name]
    boolean exists = false
    for i ids.len (
        if ids[i].toStr() == id (
            exists = true
        )
    )
    if !exists (
        ids.append(id)
        items[name] = ids
        writeUserAlbums(username, albums)
    )
    return albums
)

def removeImageFromAlbum(string username, string name, string id) object (
    object albums = readUserAlbums(username)
    array ids = albums.items[name]
    array out = []
    for i ids.len (
        if ids[i].toStr() != id (
            out.append(ids[i])
        )
    )
    object items = albums.items
    items[name] = out
    writeUserAlbums(username, albums)
    return albums
)

def userBinPath(string username) string (
    return "db/" ++ username.toLower() ++ "/bin.json"
)

def ensureUserBin(string username) (
    string dir = "db/" ++ username.toLower()
    if !fs.Exists(dir) (
        fs.MkdirAll(dir)
    )
    string path = userBinPath(username)
    if !fs.Exists(path) (
        fs.WriteFile(path, "[]")
    )
)

def readUserBin(string username) array (
    ensureUserBin(username)
    string path = userBinPath(username)
    string data = fs.ReadFile(path)
    if data == "" (
        return []
    )
    array arr = data.JsonParse()
    return arr
)

def writeUserBin(string username, array arr) boolean (
    string path = userBinPath(username)
    string out = arr.toStr()
    return fs.WriteFile(path, out)
)

def calculateStorageStats(string username) object (
    string path = "db/" ++ username.toLower() ++ "/blob"
    if !fs.Exists(path) (
        return { totalBytes: 0, imageCount: 0, largestImages: [], binBytes: 0, duplicateGroups: [] }
    )
    array files = fs.ReadDir(path)
    number totalBytes = 0
    int imageCount = 0
    array entries = []
    object sizeGroups = {}
    
    for i files.len (
        string name = files[i].toStr()
        if name.endsWith(".jpg") (
            string fpath = path ++ "/" ++ name
            object stat = fs.GetStat(fpath)
            number size = stat.size.toNum()
            totalBytes = totalBytes + size
            imageCount = imageCount + 1
            string id = name.trim(1, -5)
            
            string sizeStr = size.toStr()
            if sizeGroups[sizeStr] == null (
                sizeGroups[sizeStr] = []
            )
            array sg = sizeGroups[sizeStr]
            sg.append({ id: id, path: fpath })
            sizeGroups[sizeStr] = sg
            
            entries.append({ id: id, bytes: size, path: fpath })
        )
    )
    
    // Detect duplicates
    object hashGroups = {}
    array duplicateGroups = []
    
    array sizeKeys = sizeGroups.getKeys()
    for i sizeKeys.len (
        string sKey = sizeKeys[i]
        array group = sizeGroups[sKey]
        if group.len > 1 (
            for j group.len (
                object it = group[j]
                string h = calculateFileHash(it.path.toStr())
                if hashGroups[h] == null (
                    hashGroups[h] = []
                )
                array hg = hashGroups[h]
                hg.append(it.id.toStr())
                hashGroups[h] = hg
            )
        )
    )
    
    array hashKeys = hashGroups.getKeys()
    for i hashKeys.len (
        string hKey = hashKeys[i]
        array group = hashGroups[hKey]
        if group.len > 1 (
            duplicateGroups.append({ hash: hKey, ids: group })
        )
    )
    
    // Sort entries by size descending
    int n = entries.len
    for i n (
        for j (n - i - 1) (
            if entries[j].bytes.toNum() < entries[j + 1].bytes.toNum() (
                object tmp = entries[j]
                entries[j] = entries[j + 1]
                entries[j + 1] = tmp
            )
        )
    )
    
    array largest = []
    int limit = 10
    if n < limit (
        limit = n
    )
    for i limit (
        largest.append({ id: entries[i].id, bytes: entries[i].bytes })
    )
    
    string binPath = "db/" ++ username.toLower() ++ "/bin"
    number binBytes = 0
    if fs.Exists(binPath) (
        array binFiles = fs.ReadDir(binPath)
        for i binFiles.len (
            string bname = binFiles[i].toStr()
            if bname.endsWith(".jpg") (
                string bfpath = binPath ++ "/" ++ bname
                object bstat = fs.GetStat(bfpath)
                binBytes = binBytes + bstat.size.toNum()
            )
        )
    )
    
    object fileSizes = {}
    for i entries.len (
        fileSizes[entries[i].id.toStr()] = entries[i].bytes
    )
    
    return {
        totalBytes: totalBytes,
        imageCount: imageCount,
        largestImages: largest,
        binBytes: binBytes,
        duplicateGroups: duplicateGroups,
        fileSizes: fileSizes
    }
)

def userSharesPath(string username) string (
    return "db/" ++ username.toLower() ++ "/shares.json"
)

def ensureUserShares(string username) (
    string dir = "db/" ++ username.toLower()
    if !fs.Exists(dir) (
        fs.MkdirAll(dir)
    )
    string path = userSharesPath(username)
    if !fs.Exists(path) (
        fs.WriteFile(path, "{ \"shares\": [] }")
    )
)

def readUserShares(string username) object (
    ensureUserShares(username)
    string path = userSharesPath(username)
    string data = fs.ReadFile(path)
    if data == "" (
        return { shares: [] }
    )
    object obj = data.JsonParse()
    if obj.shares == null (
        obj.shares = []
    )
    return obj
)

def writeUserShares(string username, object sharesObj) boolean (
    string path = userSharesPath(username)
    return fs.WriteFile(path, sharesObj.toStr())
)

def addShare(string owner, string imageId, string targetUsername) object (
    object sharesObj = readUserShares(owner)
    array shares = sharesObj.shares
    
    boolean found = false
    for i shares.len (
        object share = shares[i]
        if share.imageId.toStr() == imageId (
            array sharedWith = share.sharedWith
            boolean exists = false
            for j sharedWith.len (
                if sharedWith[j].toStr().toLower() == targetUsername.toLower() (
                    exists = true
                )
            )
            if !exists (
                sharedWith.append(targetUsername.toLower())
                share.sharedWith = sharedWith
            )
            found = true
        )
    )
    
    if !found (
        shares.append({ imageId: imageId, sharedWith: [targetUsername.toLower()] })
    )
    
    sharesObj.shares = shares
    writeUserShares(owner, sharesObj)
    return sharesObj
)

def removeShare(string owner, string imageId, string targetUsername) object (
    object sharesObj = readUserShares(owner)
    array shares = sharesObj.shares
    array newShares = []
    
    for i shares.len (
        object share = shares[i]
        if share.imageId.toStr() == imageId (
            array sharedWith = share.sharedWith
            array newSharedWith = []
            for j sharedWith.len (
                if sharedWith[j].toStr().toLower() != targetUsername.toLower() (
                    newSharedWith.append(sharedWith[j])
                )
            )
            if newSharedWith.len > 0 (
                share.sharedWith = newSharedWith
                newShares.append(share)
            )
        ) else (
            newShares.append(share)
        )
    )
    
    sharesObj.shares = newShares
    writeUserShares(owner, sharesObj)
    return sharesObj
)

def setPublicShare(string owner, string imageId, boolean isPublic) object (
    object sharesObj = readUserShares(owner)
    array shares = sharesObj.shares
    boolean found = false
    for i shares.len (
        object share = shares[i]
        if share.imageId.toStr() == imageId (
            share["isPublic"] = isPublic
            found = true
        )
    )
    if !found and isPublic == true (
        shares.append({ imageId: imageId, sharedWith: [], isPublic: true })
    )
    sharesObj.shares = shares
    writeUserShares(owner, sharesObj)
    return sharesObj
)

def getSharedWithMe(string username) array (
    array results = []
    string dbPath = "db"
    if !fs.Exists(dbPath) (
        return results
    )
    array dirs = fs.ReadDir(dbPath)
    for i dirs.len (
        string ownerDir = dirs[i].toStr()
        string sharesPath = dbPath ++ "/" ++ ownerDir ++ "/shares.json"
        if fs.Exists(sharesPath) (
            string data = fs.ReadFile(sharesPath)
            if data != "" (
                object sharesObj = data.JsonParse()
                array shares = sharesObj.shares
                if shares != null (
                    for j shares.len (
                        object share = shares[j]
                        array sharedWith = share.sharedWith
                        for k sharedWith.len (
                            if sharedWith[k].toStr().toLower() == username.toLower() (
                                results.append({
                                    owner: ownerDir,
                                    imageId: share.imageId
                                })
                            )
                        )
                    )
                )
            )
        )
    )
    return results
)

def servePreview(*gin.Context c, string path) boolean (
    if !fs.Exists(path) or fs.IsDir(path) (
        c.JSON(404, { ok: false, error: "not found" })
        return false
    )
    
    object fileInfo = fs.GetStat(path)
    string etag = "preview-" ++ fileInfo.modTime.toStr()
    string ifNoneMatch = c.GetHeader("If-None-Match")
    if ifNoneMatch == etag (
        c.Status(304)
        return true
    )
    
    c.Header("ETag", etag)
    c.Header("Cache-Control", "private, must-revalidate")
    
    number fileSize = fileInfo.size.toNum()
    if fileSize == 0 (
        c.JSON(404, { ok: false, error: "not found" })
        return false
    )
    if fileSize > 10000000 (
        c.JSON(413, { ok: false, error: "file too large" })
        return false
    )
    
    im := img.Open(path)
    if im == null (
        c.JSON(400, { ok: false, error: "invalid image" })
        return false
    )
    defer im.Close()
    
    number w = im.Width()
    number h = im.Height()
    number maxw = 200
    number maxh = 200
    
    if w <= maxw and h <= maxh (
        byte[] out = img.EncodeJPEGBytes(im, 80)
        if out.len == 0 (
            c.JSON(500, { ok: false, error: "encode failed" })
            return false
        )
        c.Data(200, "image/jpeg", out)
        return true
    )
    
    number ratio = min(maxw / w, maxh / h)
    int rw = round(w * ratio)
    int rh = round(h * ratio)
    
    resized := img.Resize(im, rw, rh)
    if resized == null (
        c.JSON(500, { ok: false, error: "resize failed" })
        return false
    )
    defer resized.Close()
    
    im.Close()
    
    byte[] out = img.EncodeJPEGBytes(resized, 80)
    if out.len == 0 (
        c.JSON(500, { ok: false, error: "encode failed" })
        return false
    )
    c.Data(200, "image/jpeg", out)
    return true
)